---
title: "Tcl 01"
excerpt: "Introduction to the Tcl Language"
date: 2023-04-25 22:00:00 +0900
classes: wide
header:
  overlay_image: /assets/images/unsplash-emile-perron.jpg
  overlay_filter: 0.5
  caption: "Photo by [**Emile Perron**](https://unsplash.com/@emilep) on [**Unsplash**](https://unsplash.com/)"
categories:
  - Tcl
---

## Introduction

앞으로 사용할 커맨드 표기 방식은 다음과 같다.

- `command`: 커맨드
- `subcommand`: 서브 커맨드
- `-option`: 옵션
- `argument`: 커맨드 인자
- `?-option?`: 필수는 아닌 옵션
- `?argument?`: 필수는 아닌 인자

예: `puts ?-nonewline? ?channel? outputString`

### Syntax

Tcl은 position-based language이다. Tcl 커맨드 라인의 첫 단어는 반드시 Tcl 커맨드가 온다(built-in 커맨드, 프로시저 이름, 혹은 외부 커맨드). 그 후 옵션이나 서브 커맨드, 인자가 오고 줄바꿈이나 세미콜론이 오면 커맨드가 끝난다.  

- 커맨드라인의 첫 단어는 커맨드 이름이 온다.
- 각 단어는 하나 이상의 스페이스로 구분된다.
- `""`나 `{}`로 단어들을 하나의 String으로 그룹화한다. `""`의 경우 Substitution이 일어나며, `{}`의 경우 Substitution이 일어나지 않으며 줄바꿈이 커맨드 종료로 동작하지 않는다.
- 리스트는 {*}로 그룹 해제할 수 있다.
- 커맨드는 줄바꿈이나 세미콜론으로 끝난다.
- 달러 표시($)로 시작하는 단어는 변수명이어야 한다. 이는 해당 변수로 대체된다.
- 변수명 뒤에 괄호 안에 (변수)가 오면 associative array이다. `arrayName(index)`
- `[]`로 감싸진 단어들은 정규 Tcl 커맨드여야한다. 이는 해당 커맨드를 evaluation한 결과로 대체된다.
- `\`는 하나의 문자를 Escaping하여 해당 문자를 특수한 문자로 사용하지 않게 한다.
- `#`는 라인의 나머지 부분을 주석처리한다. 이는 커맨드가 올 수 있는 자리에 사용해야 한다(첫 글자 혹은 세미콜론 다음 첫 글자)

### Grouping Words

Word들 사이의 Space가 중요하다. Tcl은 키워드를 사용하지 않기 때문에 공백으로 심볼들을 체크한다. Tcl은 공백을 이용해서 커맨드, 서브커맨드, 옵션, 데이터를 구분한다. 만약 data string이 하나의 set으로 다루어져야 한다면, 따옴표(`""`)나 중괄호(`{}`)를 통해 그루핑되어야 한다.

```tcl
set x 3;

# 정상 동작 코드
if { $x > 2 } {
    set greater true;
}

# invalid command name "if{" 에러 발생
if{ $x > 2} {
    set greater true;
}

# extra characters after close-brace 에러 발생
if {$x > 2}{
    set greater true;
}

set x "a b"
puts $x
#a b

set x {a b}
puts $x
#a b

set x a b
# Error

# Comment
puts "text"; # Command

puts "text" # Error
```

### Data Representation

Tcl은 변수를 사용전에 선언해둘 필요는 없다. 변수명은 어떤 문자든 사용할 수 있으나, C와 유사한 컨벤션을 따는 것이 권장된다. 

```tcl
# x 변수의 값을 four로 세팅
set x four
# pi 변수의 값을 3.15로 세팅
set pi 3.14
# "pi is 3.14"
puts "pi is $pi"
# pi*2 변수를 3.14로 세팅
set pi*2 6.28
# 
set "bad varname" "Don't Do This"
```

## COMMAND EVALUATION AND SUBSTITUTIONS

Tcl의 Evaluation 과정은 직관적이다. 하지만 어떻게 동작하는지 이해하지 못하면 당황스러울 수 있다. Tcl은 두 스텝으로 나뉘어서 커맨드를 처리한다. 먼저, Command와 Variable substitution이 일어난다. 그 후 결과 String을 Evaluation한다. 내장커맨드와 사용자 정의 서브루틴 모두 동일한 과정을 거친다. 예를 들어 `while` 커맨드는 두 개의 인자(조건, Body)를 받는다.

### Substitution

- 커맨드 처리의 첫 과정은 Substitution이다. Tcl 인터프리터는 커맨드를 왼쪽에서 오른쪽으로 스캔한다. 스캔하는 동안 Substitution되어야 하는 부분을 적절한 값으로 대체한다. Substitution은 다음과 같은 경우에 일어난다.
  - `[...]`로 감싸진 Tcl 커맨드
  - `$`로 시작하는 변수

### Controlling Substitutions with Quote, Curly Braces, and the Backslash

대부분의 Tcl 커맨드는 정해진 숫자의 인자를 받으며, 잘못된 숫자의 인자가 사용되면 에러가 발생한다. 만약 여러 단어로 구성된 하나의 인자를 사요하고 싶다면 따옴표(`""`)나 중괄호(`{}`)를 사용하여 그룹화해야 한다. 백슬래시(`\`)를 사용하면 특수 문자를 escaping할 수 있다. 

```tcl
set x 2
set y 3
puts {The sum of $x and $y is [expr $x+$y]}
# The sum of $x and $y is [expr $x+$y]
puts "The sum of $x and $y is [expr $x+$y]"
# The sum of 2 and 3 is 5
puts "The sum of \$x and \$y is \[expr $x+$y\]"
# The sum of $x and $y is [expr 2+3]
```

#### Splitting Lists

`{*}` 연산자는 리스트를 컴포넌트 파트들로 변환한다. 이는 어떤 프로시저의 리턴값을 다른 프로시저에 하나의 리스트가 아니라 각각의 분리된 값으로 전달해야할 때 주로 사용된다. `set` 커맨드는 두 개의 인자가 필요하다(변수명, 값).

```tcl
set nameANDvalue "a 2"
# Error: can't read "a 2": no such variable
set $nameANDvalue

set {*}$nameANDvalue
puts $a
# 2
```

## DATA TYPES

Tcl의 primitive data type은 String이다(숫자도 가능). Composite data type은 list, dict, associative array이다. Tcl 인터프레티너느 그래픽 오브젝트, I/O 채널, 소캣 등의 복잡한 개체도 사용할 수도 있다. C, C++, Java와 다르게 Tcl은 typeless 언어이다. 하지만 커맨드는 인자의 타입을 체크하고, 타입이 맞지 않으면 에러를 발생시킬수 있다.

### Assigning Values to Variables

- `set varName ?value`
  - `set`은 항상 variable의 값을 리턴한다. 만약 `set`에 두 개의 argument가 있으면 첫 argument는 변수명이고 두 번째는 변수에 할당할 값이다. 만약 `set`에 하나의 argument만 있으면 변수의 값을 리턴한다.
- `append varName ?value1? ?value2?`
  - Tcl은 주로 String 처리 언어이기 때문에, 새로운 Character를 String에 추가하는 것은 유용하다.

```tcl
set x 1
# 1
append x 2
# 12
append x
# 12
append x 3 4
# 1234
append y new value
# newvalue
```

### Strings

Tcl은 모든 데이터를 String으로 표현한다. Tcl string은 문자열, 숫자, Boolean, Binary data를 포함할 수 있다. Tcl은 문자열 표현에 16-bit Unicode를 사용한다. Boolean value로는 1이나 0을 사용할 수도 있고 string `"true"` 혹은 `"yes"`와 `"false"` 혹은 `"no"`를 사용할 수도 있다. `"TrUe"`와 같이 대소문자 구별없이 사용하는 것도 가능하다. 

```tcl
# Legitimate Strings
set alpha "abcdefg"
set validString "this is a valid string"
set number 1.234
set octalVal 0755
set hexVal 0x1ed
set scientificNotation 2e2
set msg {Bad input: "Bogus". Try again.}
set msg "Bad input: \"Bogus\". Try again."

# Bad Strings
set msg "Bad input: "Bogus". Try again."
set badstring "abcdefg
set mismatch {this is not a valid string"
set noquote this is not a valid string
```

### String Processing Commands

`string`, `foramt`, `scan` 커맨드로 대부분의 String 관련 연산을 수행할 수 있다.

- `string match pattern string`
  - 리턴: `string`과 `pattern`이 매치되면 1, 아니면 0
  - `pattern`: `string`과 비교할 pattern
  - `string`: `pattern`과 매치될 string

```tcl
set str "This is a test. it is only a test."
string match "*test*" $str
string match "not present" $str
```

- `string tolower string`
  - `string`: 소문자로 변환할 string

- `string toupper string`
  - `string`: 대문자로 변환할 string

- `string first substr string`
  - 리턴: `string`에서 첫 번째로 나타나는 `substr`의 인덱스(없으면 -1)

- `string last substr string`
  - 리턴: `string`에서 마지막으로 나타나는 `substr`의 인덱스(없으면 -1)

- `string length string`
  - 리턴: `string`의 길이

- `string range string first last`
  - 리턴: `string`의 `first`부터 `last`까지의 부분 문자열

- `string map map string`
  - 리턴: `string`에서 `map`에 정의된 문자열을 치환한 결과
  - `map`: 치환할 string pair(old, new)를 담고 있는 set

```tcl
set str "This is a test. it is only a test."

set upper [string toupper $str]
puts $upper
# THIS IS A TEST. IT IS ONLY A TEST.
set lower [string tolower $upper]
puts $lower
# this is a test. it is only a test.
set st_first [string first "test" $str]
puts $st_first
# 10
set st_last [string last "test" $str]
puts $st_last
# 29
set len [string length $str]
puts $len
# 34
set subset [string range $str $st_first $st_last]
puts $subset
# test. it is only a t
set mapped [string map {"a test" "an exam"} $str]
puts $mapped
# This is an exam. it is only an exam.
```

- `string is type string`
  - 리턴: `string`이 `type`이면 1, 아니면 0
  - `type`: `digit`, `integer`, `double`, `alnum`, `upper`, `lower`, `space`

```tcl
puts [string is integer "123"]
# 1
puts [string is integer "123a"]
# 0
puts [string is integer "123.45"]
# 0
puts [string is double "123.45"]
# 1
puts [string is alnum "123.45a"]
# 0
puts [string is alnum "12345a"]
# 1
```

- `format foramtString ?data1? ?data2? ...`
  - 포맷 문자열을 사용하여 문자열을 생성한다.

```tcl
{% raw %}
puts [format {%5.3f} [expr 2.0/3]]
# 0.667
puts [format {%c%c%c%c%c} 72 101 108 108 111]
# Hello
{% endraw %}
```

- `scan textString formatString ?varName1? ... ?varNameN?`
  - text string을 variable로 parsing한다.

```tcl
set string {Speak, Friend and Enter}
scan $string {Speak, %s %s %s} a b c
puts "The Password is: $a"
# The Password is: Friend
```

```tcl
{% raw %}
# Define the string to parse.
set logEntry {Mar 25 14:52:50 clif sendmail[23755]:
from=<tcl-core-admin@lists.sourceforge.net>,
size=35362, class=-60, nrcpts=1}
# Extract "From" using string first and string range
set openAnglePos [string first "<" $logEntry]
set closeAnglePos [string first ">" $logEntry]
set fromField [string range $logEntry $openAnglePos $closeAnglePos]
# Extract the date using scan
scan $logEntry {%s %d %d:%d} mon day hour minute
# Extract the size using scan and string cmds.
set sizeStart [string first "size=" $logEntry]
set substring [string range $logEntry $sizeStart end]
# The formatString looks for a word composed of the
# letters ‘eisz’ (size will match) followed by an
# equals sign, followed by an integer. The word
# ‘size’ gets placed in the variable discard,
# and the numeric value is placed in the variable
# sizeField.
scan $substring {%[eisz]=%d} discard sizeField
puts [format {%-12s %-40s %-s} "Timestamp" "From" "Size"]
puts [format {%s %d %d:%d %-40s %d} \
$mon $day $hour $minute $fromField $sizeField]

# Timestamp    From                                   Size
# Mar 25 14:52 <tcl−core−admin@lists.sourceforge.net> 35362
{% endraw %}
```

### Lists

- Tcl list는 문법적인 컨벤션을 다르면 string으로 표현 가능하다. (내부적으로 string은 Tcl object에 대한 포인터 리스트로 표현된다.)
- list는 중괄호(`{}`) 안에 리스트 원소들을 써서 표현할 수 있다.
- 각 word는 리스트 원소가 된다.
- 하나의 word 그룹은 중괄호 `{}`로 묶을 수 있다.
- list 원소가 비어있을 수도 있다. `{}`로 표현된다.

```tcl
{This is a six element list}
{This list has {a sublist} in it}
{Lists may {be nested {arbitrarily deep}}}
"A string like this may be treated as a list"
```

### List Processing Commands

- `set` 커맨드를 사용해서 리스트를 변수에 할당할 수 있다.
- `list` 커맨드를 여러 인자들을 하나의 리스트 원소에 그룹화할 수 있다.
- `lappend` 커맨드로 데이터를 추가할 수 있다.
- `split` 커맨드로 하나의 인자를 리스트 원소들로 나눌수 있다.

- `list ?element1? ?element2? ... ?elementN?`
  - 리턴: 인자들을 원소로 하는 list
  - 인자가 없으면 빈 string을 리턴한다.

- `lappend listName ?element1? ... ?elementN?`
  - 리턴: 인자들을 리스트에 추가한 리스트를 반환한다. 리스트가 기존에 존재했다면 수정된다.

```tcl
set mylist [list first second [list three element sublist] fourth]
puts $mylist
# first second {three element sublist} fourth
lappend mylist fifth
puts $mylist
# first second {three element sublist} fourth fifth
```

- `split data ?splitChar?`
  - 리턴: `data`를 `splitChar`로 나눈 리스트를 반환한다. `splitChar`가 없으면 공백으로 나눈다.

- `join list ?separator?`
  - 리스트 원소들을 join한다. `separator`가 없으면 공백으로 join한다.

- `llength list`
  - 리스트의 길이를 리턴한다.

- `lsearch ?-option? list pattern`
  - `pattern`과 매칭되는 첫 리스트 원소의 인덱스를 반환한다. 만약 없으면 -1을 반환한다.

```tcl
set mylist [list first second [list three element sublist] fourth]
lsearch $mylist second
# 1
```

- `lindex list index`
  - 리턴: `list`의 `index`번째 원소를 리턴한다. `index`는 0부터 시작한다.

- `linsert list position element1 ... ?elementN?`
  - 리턴: 리스트에 `position` 위치에 원소들이 추가된 리스트를 반환한다. 기존 리스트를 수정하지는 않는다.

- `lreplace list first last element1 ... ?elementN?`
  - 리턴: 리스트 원소들이 replace된 리스트를 반환한다. 기존 리스트를 수정하지는 않는다.
  - `lreplace` 커맨드를 통해 리스트의 원소를 늘리거나 줄이는데 사용할 수도 있다.

```tcl
set mylist [list first second [list three element sublist] fourth]
# first second {three element sublist} fourth
set newlist [lreplace $mylist 0 0 one]
# one second {three element sublist} fourth
set shortlist [lreplace $mylist 0 1]
# {three element sublist} fourth
```

#### List Example

```tcl
# Define the raw data
set rawData {Package,Major,Minor,Patch
Tcl,8,6,0
math::geometry,1,0,3
math::complexnumbers,1,0,2}
# Split the raw data into a list using the newlines
# as list element separators.
# This creates a list in which each line becomes a
# list element
set dataList [split $rawData "\n"]
# Create a list of the column headers.
set columns [split [lindex $dataList 0] ","]
foreach line [lrange $dataList 1 end] {
    # Convert the line of data into a list
    set rowValues [split $line ","]
    # Create a new list from $rowValues that includes
    # all the elements after the first (package name).
    set revList [lrange $rowValues 1 end]
    # and rejoin them into a "." separated string
    set revision [join $revList "."]
    # Display a reformatted version of the data line
    puts [format "%s: %s Revision: %s" [lindex $columns 0]\
    [lindex $rowValues 0] $revision]
}

# Package: Tcl Revision: 8.6.0
# Package: math::geometry Revision: 1.0.3
# Package: math::complexnumbers Revision: 1.0.2
```

### Dictionaries

`dict` 커맨드는 Tcl 8.5 버전부터 추가되었다. Dictionary는 컨셉적으로는 key-value pair로 구성된 순서가 있는 리스트로, 다음과 같은 특징이 있다.

- dict는 중첩된 딕셔너리를 지원한다.
- `dict` 커맨드는 C code hash table로 구현되어 있다.
- `dict`는 다양한 관련 서브커맨드를 지원한다.

- `dict create ?key1 value1 ... keyN valueN?`
  - 리턴: key-value pair로 구성된 dictionary를 리턴한다.

- `dict append dictName key value`
  - value를 주어진 key에 추가한다(string append 방식). 기존 dictionary를 수정하며, 없다면 새로 생성한다.

- `dict lappend dictName key value`
  - value를 주어진 key에 추가한다(list append 방식). 기존 dictionary를 수정하며, 없다면 새로 생성한다.

- `dict replace $dict key new_value`
  - key가 new_value로 replace된 새 dictionary를 리턴한다. 기존 dictionary를 수정하지는 않는다.

- `dict set dictName key new_value`
  - `dict set` 커맨드로 바로 수정할 수도 있다.

## ARITHMETIC AND BOOLEAN EXPRESSIONS

### Math Operations

- Math operation은 `expr`과 `incr` 커맨드로 수행한다. `expr` 커맨드는 일반적인 목적의 계산 엔진을 제공하며, `incr` 커맨드는 변수의 값을 증가시키는데 사용한다.
- 일반적인 Tcl 커맨드와 달리 `expr` 커맨드는 고정된 갯수의 인자를 받지 않는다.
- Tcl은 다음과 같은 연산자를 지원한다.
  - `- + ~ !`: Unary minus, plus, bitwise NOT, logical NOT
  - `* / %`: Multiplication, division, remainder
  - `+ -`: Addition, subtraction
  - `<< >>`: Left shift, right shift
  - `< > <= >=`: Less than, greater than, less than or equal, greater than or equal
  - `== !=`: Equal, not equal
  - `& ^ |`: Bitwise AND, bitwise exclusive OR, bitwise OR
  - `&& ||`: Logical AND, logical OR
  - `x?y:z`: If x is true, return y, else return z
- 기타 커맨드: `sin`, `cos`, `tan`, `asin`, `acos`, `atan`, `sinh`, `cosh`, `tanh`, `hypot`, `atan2`, `log`, `log10`, `sqrt`, `exp`, `pow`, `round`, `floor`, `ceil`, `fmod`, `double`, `int`, `abs`, `srand`, `rand`

### Conditionals

#### The `if` Command

```tcl
if {testExpression} {
  body1
} ?elseif {testExpression2} {
  body2
} ?else {
  bodyN
}?
```

- Expression을 평가하여 true이면 body를 수행한다. expression은 `expr` 커맨드에 사용할 수 있는 형식이어야 한다.
  - `{$a < 2>}`
  - `{ $string ne "OK"}`
  - `{ [eof $inputFile]}`

#### The `switch` Command

```tcl
switch ?opt? str pat1 bod1 ?pat2 bod2 ...? ?default defaultBody?
```

- `?opt?`: `-exact`, `-glob`, `-regexp` 옵션이 가능하다. 기본 옵션은 `-glob`이다.

```tcl
set x 7
set y 7
# Using no braces substitution occurs before the switch
# command looks for matches.
# Thus a variable can be used as a match pattern:
switch $x \
    $y {puts "X=Y"} \
    {[0-9]} {puts "< 10"} \
    default {puts "> 10"}
# >>> X=Y

# With braces, the $y is not substituted to 7, and switch looks
# for a match to the literal string $y
switch -glob $x {
    "1" {puts "one"}
    "2" {puts "two"}
    "3" {puts "three"}
    "$y" {puts "X=Y"}
    {[4-9]} {puts "greater than 3"}
    default {puts "Not a value between 1 and 9"}
}
# >>> greater thant 3
```

### Looping

#### The `for` Command

- `for start test next body`

```tcl
for {set i 0} {$i < 2} {incr i} {
  puts "I is: $i"
}
# >>> I is: 0
# >>> I is: 1
```

#### The `while` Command

- `while test body`

```tcl
set x 0;
while {$x < 5} {
    set x [expr $x+$x+1]
    puts "X: $x"
}

# >>> X: 1
# >>> X: 3
# >>> X: 7
```

#### The `foreach` Command

- `foreach listVar list body`
  - item list를 iterate할 때 사용한다. 

```tcl
set total 0
foreach num {1 2 3 4 5} {
  set total [expr $total + $num]
}
puts "The total is: $total"
# >>> 15
```

- `foreach valueList dataList1 ?valueList2 dataList2? ... {
    body
  }`
  - 여러개의 리스트를 iterate할 때 사용한다.

```tcl
foreach {pres date} { {George Washington} {1789-1797}
                      {John Adams} {1797-1801}
                      {Thomas Jefferson} {1801-1809}
                      {James Madison} {1809-1817}
                      {James Monroe} {1817-1825}
    } state {Virginia Massachusetts Virginia Virginia Virginia} {
    puts "$pres was from $state and served from $date"
}

# >>> George Washington was from Virginia and served from 1789-1797
# >>> John Adams was from Massachusetts and served from 1797-1801
# >>> Thomas Jefferson was from Virginia and served from 1801-1809
# >>> James Madison was from Virginia and served from 1809-1817
# >>> James Monroe was from Virginia and served from 1817-1825
```

### Exception Handling in Tcl

- `catch script ?varName?`: 에러 캐치
- `error informationalString ?info? ?Code?`: 에러 발생

## MODULARIZATION

- Subroutines(`proc`)
- Multiple source files(`source`)
- Libaries(`package`)

### Procedures

- 코드 모듈화를 위한 흔한 테크닉이다.
  - Recusive하게 수행 가능
  - 특정 인자들을 받을 수 있음
  - 인자들의 default value 설정 가능
  - 가변 갯수의 인자를 받을 수도 있음

- `proc procName argList body`

```tcl
# Define the classic recursive procedure to find the
# n’th position in a Fibonacci series.
proc fib {num} {
    if {$num <= 2} {return 1}
    return [expr [fib [expr $num -1]] + [fib [expr $num -2]] ]
}

for {set i 1} {$i < 6} {incr i} {
    puts "Fibonacci series element $i is: [fib $i]"
}

# >>> Fibonacci series element 1 is: 1
# >>> Fibonacci series element 2 is: 1
# >>> Fibonacci series element 3 is: 2
# >>> Fibonacci series element 4 is: 3
# >>> Fibonacci series element 5 is: 5
```

### Loading Code from a Script File

밀접하게 연관된 프로시저들을 각각의 파일로 분리할 수 있고, 이를 통해 유지보수를 쉽게 만들 수 있다. `source` 커맨드는 파일을 현재 스크립트에 로드하는 커맨드이다. C의 `#include`와 유사하다.

- `source fileName`

